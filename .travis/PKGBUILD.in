# Maintainer: Pier Luigi Fiorini <pierluigi.fiorini@gmail.com>

pkgname=libqmlos-git
pkgver=@PKGVER@
pkgrel=1
pkgdesc="A collection of core classes used throughout qmlOS"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/qmlos"
license=('LGPL3')
depends=('qt5-base' 'qt5-declarative' 'kwallet' 'solid' 'networkmanager-qt'
         'modemmanager-qt' 'pulseaudio' 'libqtxdg' 'fluid-git')
makedepends=('git' 'extra-cmake-modules')
conflicts=('libhawaii' 'libhawaii-git' 'libqmlos')
replaces=('libhawaii' 'libhawaii-git' 'libqmlos')
provides=('libqmlos')
groups=('qmlos-git')

_gitroot="git://github.com/qmlos/libqmlos.git"
_gitbranch=@GITBRANCH@
_gitname=libqmlos
source=(${_gitname}::${_gitroot}#branch=${_gitbranch})
md5sums=('SKIP')

pkgver() {
	cd ${srcdir}/${_gitname}
	echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../${_gitname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DLIB_INSTALL_DIR=lib \
		-DLIBEXEC_INSTALL_DIR=lib \
		-DQML_INSTALL_DIR=lib/qt/qml \
		-DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
